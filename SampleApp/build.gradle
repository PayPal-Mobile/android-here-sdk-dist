import java.util.regex.Pattern

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
        google()
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
        classpath 'com.android.tools.build:gradle:3.4.1'
    }
}
apply plugin: 'com.android.application'

repositories {
    jcenter()
    maven {
        url 'https://maven.google.com'
    }
}

android {
    packagingOptions {
        exclude("META-INF/*.kotlin_module")
    }
    
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.paypal.heresdk.sampleapp"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        ndk {
            abiFilters "armeabi", "armeabi-v7a", "x86", "mips"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            //Don't change the value of release notes as we are searching for the value and replacing it with actual release notes in build jobs
            ext.betaDistributionReleaseNotes = 'External SDK Release 1.6.8'
            ext.betaDistributionGroupAliases = 'paypalhere-sdk'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    repositories {
        mavenCentral()
        jcenter()
    }
    api("com.paypal.retail:here-sdk-debug:$PUBLISH_VERSION@aar") { transitive = true }

    // Dagger core
    annotationProcessor 'com.google.dagger:dagger-compiler:2.13'
    implementation 'com.google.dagger:dagger:2.13'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-runtime:2.1.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.1.0'

    implementation 'androidx.lifecycle:lifecycle-extensions:2.1.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.1.0'

    // ReactiveX
    implementation 'io.reactivex.rxjava2:rxjava:2.1.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    // Android Annotation
    annotationProcessor 'org.androidannotations:androidannotations:4.4.0'
    implementation 'org.androidannotations:androidannotations-api:4.4.0'

    //Android & Android X
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.fragment:fragment:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'com.google.android.material:material:1.0.0-rc01'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.0.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android'

    implementation 'com.eclipsesource.j2v8:j2v8:4.6.0@aar'
}

task incrementVersionCode {
    doLast {
        def manifestFile = file("src/main/AndroidManifest.xml")
        def patternVersionCode = Pattern.compile("versionCode=\"(\\d+)\"")
        def manifestText = manifestFile.getText()
        def matcherVersionCode = patternVersionCode.matcher(manifestText)
        matcherVersionCode.find()
        def mVersionCode = Integer.parseInt(matcherVersionCode.group(1))
        def mNextVersionCode = mVersionCode + 1
        def manifestContent = matcherVersionCode.replaceAll("versionCode=\"" + mNextVersionCode + "\"")
        manifestFile.write(manifestContent)
    }
}
